plugins {
    id 'cpp'
    id 'java'
    id 'google-test'
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
    id 'edu.wpi.first.NativeUtils' version '2021.0.5'
    id 'edu.wpi.first.GradleJni' version '0.11.0'
    id 'edu.wpi.first.GradleVsCode' version '0.12.0'
}

allprojects {
    repositories {
        mavenCentral()
    }
    if (project.hasProperty('releaseMode')) {
        wpilibRepositories.addAllReleaseRepositories(project)
    } else {
        wpilibRepositories.addAllDevelopmentRepositories(project)
    }
}

/******************************************************
 * Build options
 * -PreleaseMode will force only release versions of the wpilib repo
 *
 * -Ponly<platform> will only build for a specific platform, list:
 * 
 * linuxathena
 * linuxraspbian
 * linuxaarch64bionic
 * windowsx86-64
 * windowsx86
 * linuxx86-64
 * osxx86-64
 ******************************************************/


apply from: 'config.gradle'

apply from: 'dependencies.gradle'

nativeUtils {
  exportsConfigs {
      AdiFrcImu {
      }
  }
  privateExportsConfigs {
    AdiFrcImuDriver {
      exportsFile = project.file("src/main/driver/symbols.txt")
    }
  }
}

model {
  components {
    AdiFrcImu(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include'
          }
        }
      }
      binaries.all {
        lib library: "AdiFrcImuDriver", linkage: 'shared'
      }


      nativeUtils.useRequiredLibrary(it, "wpilib_shared")
    }

    AdiFrcImuDriver(JniNativeLibrarySpec) {
      enableCheckTask true
      javaCompileTasks << compileJava
      jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.roborio)
      jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.raspbian)
      jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.aarch64bionic)
      sources {
        cpp {
          source {
            srcDirs 'src/main/driver/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/driver/include'
          }
        }
      }

      nativeUtils.useRequiredLibrary(it, "driver_shared")
    }
  }

  testSuites {
    AdiFrcImuTest {
        sources.cpp {
            source {
                srcDir 'src/test/native/cpp'
                include '**/*.cpp'
            }
        }

        binaries.all {
          lib library: 'AdiFrcImuDriver', linkage: 'shared'
        }

        nativeUtils.useRequiredLibrary(it, "wpilib_executable_shared", "googletest_static")
    }

    AdiFrcImuDriverTest {
        sources.cpp {
            source {
                srcDir 'src/test/driver/cpp'
                include '**/*.cpp'
            }
        }

        nativeUtils.useRequiredLibrary(it, "wpilib_executable_shared", "googletest_static")
    }
  }
}

apply from: 'publish.gradle'

wrapper {
  gradleVersion = '6.0.1'
}
